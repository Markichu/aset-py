10 PRINT"ASET-B  VERSION 1.0   WRITTEN BY W.D. WALTON  1985"
20 PRINT"CONTRIBUTION OF THE NATIONAL BUREAU OF STANDARDS (U.S.)."
30 PRINT"NOT SUBJECT TO COPYRIGHT."
40 DIM TQ(100),Q(100)
50 INPUT"ENTER RUN TITLE";TITLE$
60 LR=.35
70 INPUT"HEAT LOSS FRACTION";LC
80 INPUT"FIRE HEIGHT (ft)";F
90 INPUT"ROOM CEILING HEIGHT (ft)";H
100 INPUT"ROOM FLOOR AREA (sq ft)";A
110 FM=F*.3048
120 HM=H*.3048
130 AM=A*.0929
140 INPUT"MAXIMUM TIME (sec)";TL
150 Q0=.1
160 PRINT"INPUT FIRE TIMES AND HEAT RELEASE RATES (use -9,-9 to end input)"
170 FOR N=1 TO 100
180 INPUT"TIME (sec), HEAT RELEASE RATE (kW)";TQ(N),QI
190 IF TQ(N) < 0 GOTO 240
200 IF QI < Q0 THEN Q(N)=Q0
210 Q(N)=QI/Q0
220 NEXT N
230 PRINT"MAXIMUM OF 100 INPUT VALUES REACHED"
240 NQ=N-1
250 IF TQ(1)<.1 THEN TQ(1)=.1
260 PA=530!
270 DA=.0735
280 CP=.24
290 C1=(1!-LC)*Q0/(DA*CP*PA*A*1.054)
300 C2=.21/A*((1!-LR)*Q0*32!/(DA*CP*PA*1.054))^(1!/3!)
310 DQ0=(Q(1)-1!)/TQ(1)
320 Z0=H-F
330 Z1=Z0
340 DZ1=-C1-C2*Z0^(5!/3!)
350 P1=1!+C1/C2*Z0^(-5!/3!)
360 DP1=C1/C2*(2!*DQ0+5!*(C1+C2*Z0^(5!/3!)))/(6!*Z0^(8!/3!))
370 T1=0!
380 T2=0!
390 DT=1!
400 QT=1!
410 OI=5!
420 OT=0!
430 PRINT" "
440 PRINT"ASET-B  VERSION 1.0"
450 PRINT TITLE$
460 PRINT"HEAT LOSS FRACTION=";LC
470 PRINT"FIRE HEIGHT=";F;"ft   ";FM;"m"
480 PRINT"ROOM HEIGHT=";H;"ft   ";HM;"m"
490 PRINT"ROOM AREA=";A;"sq ft   ";AM;"sq m"
500 PRINT" "
510 PRINT"    TIME      TEMP      TEMP     LAYER     LAYER      FIRE      FIRE"
520 PRINT"     sec         F         C        ft         m        kW     BTU/s"
530 GOSUB 970
540 T1=T2
550 T2=T1+DT
560 IF T2>TL THEN PRINT"RUN COMPLETE":GOTO 870
570 IF T2>TQ(NQ) THEN PRINT"END OF INPUT FIRE":GOTO 870
580 GOSUB 880
590 Z2=Z1+DZ1*DT
600 P2=P1+DP1*DT
610 IER=0
620 IF Z2<=0! GOTO 670
630 K=C2*QT^(1!/3!)*Z2^(5!/3!)
640 DZ2=-C1*QT-K
650 DP2=P2*(C1*QT-(P2-1!)*K)/(Z0-Z2)
660 GOTO 720
670 DP2=(P2*C1*QT)/(Z0-Z2)
680 IF Z2<=-F GOTO 710
690 DZ2=-C1*QT
700 GOTO 720
710 DZ2=0!
720 Z2C=Z1+(DZ1+DZ2)/2!*DT
730 P2C=P1+(DP1+DP2)/2!*DT
740 IF Z2C<-F THEN Z2=-F:Z2C=-F
750 IF ABS(Z2C-Z2)<.001 AND ABS(P2C-P2)<.001 GOTO 810
760 IF IER>30 THEN PRINT"WARNING! SOLUTION DID NOT CONVERGE":GOTO 810
770 Z2=Z2C
780 P2=P2C
790 IER=IER+1
800 GOTO 620
810 Z1=Z2C
820 P1=P2C
830 DZ1=DZ2
840 DP1=DP2
850 GOSUB 970
860 GOTO 540 
870 END
880 IF T2>TQ(1) GOTO 910
890 QT=Q(1)-((Q(1)-1!)/TQ(1))*(TQ(1)-T2)
900 RETURN 
910 FOR N=1 TO NQ
920 IF T2>TQ(N) GOTO 950
930 QT=Q(N)-((Q(N)-Q(N-1))/(TQ(N)-TQ(N-1)))*(TQ(N)-T2)
940 RETURN
950 NEXT N
960 RETURN
970 IF T2<OT THEN RETURN
980 OT=OT+OI
990 QK=QT*Q0
1000 QB=QK*.9485
1010 PF=(P1*PA)-460!
1020 PC=(PF-32!)/1.8
1030 RRF=DP1*31800!
1040 RRC=RRF/1.8
1050 X=Z1+F
1060 XM=X*.3048
1070 PRINT USING"######.#  ";T2;PF;PC;X;XM;QK;QB
1080 RETURN